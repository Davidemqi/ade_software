000010*****************************************************************
000020 IDENTIFICATION DIVISION.
000030 PROGRAM-ID. BQVFBT.
000040 AUTHOR. N9276 - D'URSO.
000050 DATE-WRITTEN. 13/11/98.
000060*****************************************************************
000070*$1 TITOLO: VERSAMENTI UNIFICATI
000080*           STAMPA SQUADRATURE  BANCHE E CONCESSIONARI
000090*****************************************************************
000100*
000110*$1 FUNZIONI SVOLTE E LOGICA DI ELABORAZIONE
000120*
000130*   IL PROGRAMMA SI INSERISCE NEL PACCHETTO DI CONTROLLO DATI
000140*   'F24' PER MAINFRAME UTENTE.
000150*   IL PGM BQVFBT PRODUCE UNA STAMPA DELLE SQUADRATURE RILEVATE.
000160*
000170*   IL PACCHETTO DI CONTROLLO DATI PER MAINFRAME UTENTE
000180*   UTILIZZA LA STESSA LOGICA SEGUITA DALLE PROCEDURE DI
000190*   ACQUISIZIONE DATI E DEGLI ESITI PRESSO LA ANAGRAFE
000200*   TRIBUTARIA.
000210*   IL PACCHETTO CONSENTE ALL'UTENTE DI EFFETTUARE PRESSO
000220*   IL PROPRIO SISTEMA GLI STESSI CONTROLLI CHE VERRANNO
000230*   SVOLTI DALLA ANAGRAFE TRIBUTARIA DOPO L'INVIO DEI DATI,
000240*   ANTICIPANDO QUINDI L'INDIVIDUAZIONE DEGLI EVENTUALI
000250*   ERRORI.
000260*
000270*     ARCHIVI  GESTITI
000280*
000290*     NOME       DESCRIZIONE                         ACCESSO
000300*
000310*   * BQVFB5 * ARCHIVIO SQUADRATURE                  * IN * SEQ  *
000320*
000330*   * BQVFPR * ARCHIVIO 'PROVINCE'                   * IN * SEQ  *
000340*
000350*     ROUTINE  RICHIAMATE
000360*
000370*     BQVFSR   INTERROMPE L'ELABORAZIONE
000380*
000390*****************************************************************
000400     EJECT
000410*****************************************************************
000420*                ENVIRONMENT DIVISION
000430*****************************************************************
000440 ENVIRONMENT DIVISION.
000450 CONFIGURATION SECTION.
000460 SPECIAL-NAMES.
000470     DECIMAL-POINT IS COMMA.
000480 INPUT-OUTPUT SECTION.
000490 FILE-CONTROL.
000500**   SQUADRATURE
000510     SELECT BQVFB5
000520            ASSIGN TO UT-S-BQVFB5.
000530     EJECT
000540**   STAMPA
000550     SELECT BQVFBP
000560            ASSIGN TO UT-S-BQVFBP.
000570     EJECT
000580**   DECODIFICA PROVINCIA (FILE PROVINCE)
000590     SELECT BQVFPR
000600            ASSIGN TO UT-S-BQVFPR.
000610     EJECT
000620*****************************************************************
000630*                DATA DIVISION                                  *
000640*****************************************************************
000650 DATA DIVISION.
000660 FILE SECTION.
000670** ARCHIVIO DI OUTPUT DELLE SQUADRATURE
000680 FD  BQVFB5
000690     LABEL RECORD IS OMITTED
000700     BLOCK CONTAINS 0 RECORDS.
000710 01  BQVFBS2                            PIC X(7290).
000720**   STAMPA
000730 FD  BQVFBP
000740     LABEL RECORD IS OMITTED
000750     BLOCK CONTAINS 0 RECORDS.
000760 01  W-REC-STAMPA                PIC X(080).
000770**   DECODIFICA PROVINCIA
000780 FD  BQVFPR
000790     LABEL RECORD IS STANDARD
000800     BLOCK CONTAINS 0 RECORDS.
000810 01  BQVFPR2                     PIC X(042).
000820*****************************************************************
000830*                WORKING-STORAGE SECTION                        *
000840*****************************************************************
000850 WORKING-STORAGE SECTION.
000860     SKIP2
000870 77  FILLER                      PIC X(017) VALUE
000880                                 'INIZIO W-S BQVFBT'.
000890**   CODICE USER ABEND
000900 77  ABEND-CODE                  PIC X(004) VALUE ZERO.
000910**   CAMPI DI COMODO PER IL TEST DI FINE FILE
000920 77  W-EOF-BQVFB5                PIC X(001).
000930 77  W-EOF-BQVFPR                PIC X(001).
000940**   TOTALE FLUSSI LOGICI ELABORATI
000950 77  CTR-FLUSSI                  PIC 9(007).
000960**   CONTATORI STATISTICI:
000970**   RECORD LETTI DA 'SQUADRATURE'
000980 77  CTR-LETTI-BQVFB5            PIC 9(007).
000990**   RECORD SCRITTI IN STAMPA
001000 77  CTR-SCRITTI-STAMPA          PIC 9(007).
001010*    FLAG INDICANTE SE E' STATO TROVATO UN RECORD SU UN ARCHIVIO
001020*    W-FLG-TROVATO = 1 ===> TROVATO
001030*    W-FLG-TROVATO = 0 ===> NON TROVATO
001040 77  W-FLG-TROVATO               PIC 9(001).
001050**   INDICE PER LATABELLA DELLE PROVINCE SQUADRATE
001060 77  W-IND-S                     PIC 9(003).
001070*---
001080**TABELLA CONTENETE I CODICI PROVINCE PRELEVATI DAL FILE BQVFPR
001090*---
001100 01  W-TAB-PROV.
001110     03 W-TAB-PROV-NUM-MAX        PIC 9(003) VALUE 150.
001120     03 W-TAB-PROV-OCC.
001130        05 W-TAB-PROV-EL OCCURS   150 TIMES
001140                                    ASCENDING W-TAB-PROV-COD
001150                                    INDEXED BY W-PUNT-PROV.
001160           07 W-TAB-PROV-COD      PIC X(002).
001170           07 W-TAB-PROV-DESCR    PIC X(020).
001180**  TESTATE DI STAMPA
001190**  RIGA BIANCA
001200 01  W-BIANCA                    PIC X(080) VALUE SPACES.
001210**  RIGA 1
001220 01  W-TEST-1.
001230     03 FILLER                   PIC X(002) VALUE SPACES.
001240     03 FILLER                   PIC X(023)
001250               VALUE 'MINISTERO DELLE FINANZE'.
001251     03 FILLER                   PIC X(032) VALUE SPACES.
001252     03 FILLER                   PIC X(021)
001253               VALUE 'AGENZIA DELLE ENTRATE'.
001290     03 FILLER                   PIC X(002) VALUE SPACES.
001300**  RIGA 2
001310 01  W-TEST-2.
001320     03 FILLER                   PIC X(002) VALUE SPACES.
001330     03 FILLER                   PIC X(018) VALUE SPACES.
001340     03 FILLER                   PIC X(020)
001350               VALUE 'VERSAMENTI UNIFICATI'.
001360     03 FILLER                   PIC X(020)
001370               VALUE ' (mod. F24)         '.
001380     03 FILLER                   PIC X(018) VALUE SPACES.
001390     03 FILLER                   PIC X(002) VALUE SPACES.
001400**  RIGA 3
001410 01  W-TEST-3.
001420     03 FILLER                   PIC X(002) VALUE SPACES.
001430     03 FILLER                   PIC X(015) VALUE SPACES.
001440     03 FILLER                   PIC X(045) VALUE
001450                'STAMPA COMPLETA DEI RISULTATI'.
001460     03 FILLER                   PIC X(016) VALUE SPACES.
001470     03 FILLER                   PIC X(002) VALUE SPACES.
001480**  RIGA 4
001490 01  W-TEST-4.
001500     03 FILLER                   PIC X(002) VALUE SPACES.
001510**  DATA DI ELABORAZIONE (DATA DI SISTEMA)
001520     03 FILLER                   PIC X(022) VALUE
001530                     'DATA DI ELABORAZIONE: '.
001540     03 W-TEST-DATA-ELAB-GG      PIC X(002) VALUE SPACES.
001550     03 FILLER                   PIC X(001) VALUE '/'.
001560     03 W-TEST-DATA-ELAB-MM      PIC X(002) VALUE SPACES.
001570     03 FILLER                   PIC X(001) VALUE '/'.
001580     03 W-TEST-DATA-ELAB-AA      PIC X(004) VALUE SPACES.
001590     03 FILLER                   PIC X(044) VALUE SPACES.
001600     03 FILLER                   PIC X(002) VALUE SPACES.
001610**  RIGA 5
001620 01  W-TEST-5.
001630     03 FILLER                   PIC X(002) VALUE SPACES.
001640     03 FILLER                   PIC X(032)
001650               VALUE 'DATI DEL FILE LOGICO CONTROLLATO'.
001660     03 FILLER                   PIC X(044) VALUE SPACES.
001670     03 FILLER                   PIC X(002) VALUE SPACES.
001680**  RIGA 6
001690 01  W-TEST-6.
001700     03 FILLER                   PIC X(002) VALUE SPACES.
001710     03 FILLER                   PIC X(022)
001720               VALUE 'BANCA/CONC.ORDINANTE: '.
001730     03 W-TEST-ABI               PIC X(005) VALUE SPACES.
001740     03 FILLER                   PIC X(001) VALUE SPACES.
001750     03 W-TEST-DESCR-ABI         PIC X(050) VALUE SPACES.
001760**  RIGA 7
001770 01  W-TEST-7.
001780     03 FILLER                   PIC X(002) VALUE SPACES.
001790     03 FILLER                   PIC X(022)
001800               VALUE 'DATA BONIFICO       : '.
001810     03 W-TEST-DATA-BONI-GG      PIC X(002) VALUE SPACES.
001820     03 FILLER                   PIC X(001) VALUE '/'.
001830     03 W-TEST-DATA-BONI-MM      PIC X(002) VALUE SPACES.
001840     03 FILLER                   PIC X(001) VALUE '/'.
001850     03 W-TEST-DATA-BONI-AA      PIC X(004) VALUE SPACES.
001860     03 FILLER                   PIC X(044) VALUE SPACES.
001870     03 FILLER                   PIC X(002) VALUE SPACES.
001880**  RIGA 8
001890 01  W-TEST-8.
001900     03 FILLER                   PIC X(002) VALUE SPACES.
001910     03 FILLER                   PIC X(022)
001920               VALUE 'PROGR. TRASMISSIONE : '.
001930     03 W-TEST-PRG-TRASM         PIC X(002) VALUE SPACES.
001940     03 FILLER                   PIC X(052) VALUE SPACES.
001950     03 FILLER                   PIC X(002) VALUE SPACES.
001960**  RIGA 9
001970 01  W-TEST-9.
001980     03 FILLER                   PIC X(002) VALUE SPACES.
001990     03 FILLER                   PIC X(022)
002000               VALUE 'TIPO INVIO          : '.
002010     03 W-TEST-TIPO-INVIO        PIC X(012) VALUE SPACES.
002020     03 FILLER                   PIC X(042) VALUE SPACES.
002030     03 FILLER                   PIC X(002) VALUE SPACES.
002040**  RIGA 10
002050 01  W-TEST-10.
002060     03 FILLER                   PIC X(002) VALUE SPACES.
002070     03 FILLER                   PIC X(022)
002080               VALUE 'PROGRESSIVO INVIO   : '.
002090     03 W-TEST-PROG-INVIO        PIC X(002) VALUE SPACES.
002100     03 FILLER                   PIC X(052) VALUE SPACES.
002110     03 FILLER                   PIC X(002) VALUE SPACES.
002120**  RIGA 17 BIS
002130 01  W-TEST-17-BIS.
002140     03 FILLER                   PIC X(002) VALUE SPACES.
002150     03 FILLER                   PIC X(001) VALUE SPACES.
002160     03 FILLER                   PIC X(036)
002170          VALUE 'NUMERO TOTALE DI SQUADRATURE      : '.
002180     03 W-TEST-NUM-SQUADR        PIC ZZ9.
002190     03 FILLER                   PIC X(036) VALUE SPACES.
002200     03 FILLER                   PIC X(002) VALUE SPACES.
002210**  RIGA 18 BIS
002220 01  W-TEST-18-BIS.
002230     03 FILLER                   PIC X(002) VALUE SPACES.
002240     03 FILLER                   PIC X(001) VALUE SPACES.
002250     03 FILLER                   PIC X(036)
002260          VALUE 'NUMERO TOTALE DI PROVINCE PRESENTI: '.
002270     03 W-TEST-NUM-PROV          PIC ZZ9.
002280     03 FILLER                   PIC X(036) VALUE SPACES.
002290     03 FILLER                   PIC X(002) VALUE SPACES.
002300**  RIGA 19 BIS
002310 01  W-TEST-19-BIS.
002320     03 FILLER                   PIC X(002) VALUE SPACES.
002330     03 FILLER                   PIC X(001) VALUE SPACES.
002340     03 FILLER                   PIC X(036)
002350          VALUE '            DI CUI CON SQUADRATURE: '.
002360     03 W-TEST-PROV-SQUADR       PIC ZZ9.
002370     03 FILLER                   PIC X(036) VALUE SPACES.
002380     03 FILLER                   PIC X(002) VALUE SPACES.
002390**  RIGA 20 BIS
002400 01  W-TEST-20-BIS.
002410     03 FILLER                   PIC X(002) VALUE SPACES.
002420     03 FILLER                   PIC X(005) VALUE SPACES.
002430     03 FILLER                   PIC X(014)
002440          VALUE '**************'.
002450     03 FILLER                   PIC X(036)
002460          VALUE ' DETTAGLIO SQUADRATURE PER PROVINCIA'.
002470     03 FILLER                   PIC X(016)
002480          VALUE ' ***************'.
002490     03 FILLER                   PIC X(005) VALUE SPACES.
002500     03 FILLER                   PIC X(002) VALUE SPACES.
002510**  RIGA 32
002520 01  W-TEST-32.
002530     03 FILLER                   PIC X(002) VALUE SPACES.
002540     03 FILLER                   PIC X(011)
002550                            VALUE 'PROVINCIA: '.
002560     03 W-TEST-DES-PROV          PIC X(020) VALUE SPACES.
002570     03 FILLER                   PIC X(047) VALUE SPACES.
002580**  RIGA 33
002590 01  W-TEST-33.
002600     03 FILLER                   PIC X(002) VALUE SPACES.
002610     03 FILLER                   PIC X(008) VALUE SPACES.
002620     03 FILLER                   PIC X(004) VALUE 'VOCE'.
002630     03 FILLER                   PIC X(008) VALUE SPACES.
002640     03 FILLER                   PIC X(002) VALUE SPACES.
002650     03 FILLER                   PIC X(004) VALUE SPACES.
002660     03 FILLER                   PIC X(015)
002670                            VALUE 'DATO DICHIARATO'.
002680     03 FILLER                   PIC X(001) VALUE SPACES.
002690     03 FILLER                   PIC X(005) VALUE SPACES.
002700     03 FILLER                   PIC X(014)
002710                            VALUE 'DATO CALCOLATO'.
002720     03 FILLER                   PIC X(002) VALUE SPACES.
002730     03 FILLER                   PIC X(001) VALUE SPACES.
002740     03 FILLER                   PIC X(010)
002750                            VALUE 'SITUAZIONE'.
002760     03 FILLER                   PIC X(004) VALUE SPACES.
002770**  RIGA 34
002780 01  W-TEST-34.
002790     03 FILLER                   PIC X(002) VALUE SPACES.
002800     03 W-TEST-34-VOCE           PIC X(020) VALUE SPACES.
002810     03 FILLER                   PIC X(002) VALUE SPACES.
002820     03 W-TEST-34-DIC            PIC ---.---.---.---.--9.
002830     03 FILLER                   PIC X(001) VALUE SPACES.
002840     03 W-TEST-34-CALC           PIC ---.---.---.---.--9.
002850     03 FILLER                   PIC X(002) VALUE SPACES.
002860     03 W-TEST-34-SIT            PIC X(013) VALUE SPACES.
002870**   CAMPI DI COMODO PER LE DATE
002880 01  COMODO-DATA.
002890     03 DATAAA                   PIC X(002).
002900     03 DATAMM                   PIC X(002).
002910     03 DATAGG                   PIC X(002).
002920 01  DATA-SYS.
002930     03 DATA-SYS-AAAA.
002940        05 DATA-SYS-SEC          PIC X(002).
002950        05 DATA-SYS-AA           PIC X(002).
002960     03 DATA-SYS-MM              PIC X(002).
002970     03 DATA-SYS-GG              PIC X(002).
002980 01  W-DATA.
002990     03  W-AAAA                  PIC X(4).
003000     03  W-MM                    PIC X(2).
003010     03  W-GG                    PIC X(2).
003020*****************************************************************
003030*                  TRACCIATO RECORD ARCHIVI                     *
003040*****************************************************************
003050**   SQUADRATURE
003060 COPY FBQVFBS1.
003070**   PROVINCE
003080 COPY FBQVFPR1.
003090     EJECT
003100*****************************************************************
003110*                  LINKAGE SECTION                              *
003120*****************************************************************
003130 LINKAGE SECTION.
003140     EJECT
003150 PROCEDURE DIVISION.
003160*****************************************************************
003170*                PROCEDURE  DIVISION                            *
003180*****************************************************************
003190 ML-010.
003200*****DISPLAY DI INIZIO PROGRAMMA
003210     DISPLAY '************************************************'
003220     DISPLAY '*                                              *'
003230     DISPLAY '*         -    VERSAMENTI UNIFICATI   -        *'
003240     DISPLAY '*                                              *'
003250     DISPLAY '*                  STAMPA SQUADRATURE          *'
003260     DISPLAY '*                          F24                 *'
003270     DISPLAY '*                                              *'
003280     DISPLAY '************************************************'
003290     DISPLAY '*            INIZIO PROGRAMMA BQVFBT           *'
003300     DISPLAY '************************************************'.
003310*****INIZIALIZZAZIONE
003320     MOVE ZERO TO CTR-FLUSSI.
003330*****ACQUISIZIONE DATA DI SISTEMA
003340     ACCEPT COMODO-DATA FROM DATE.
003350*****FINO AL 2096 NON CI SONO PROBLEMI....
003360     IF DATAAA LESS 97
003370        GO TO ML-030.
003380 ML-020.
003390     MOVE 19 TO DATA-SYS-SEC.
003400     GO TO ML-040.
003410 ML-030.
003420     MOVE 20 TO DATA-SYS-SEC.
003430 ML-040.
003440     MOVE DATAAA TO DATA-SYS-AA.
003450     MOVE DATAMM TO DATA-SYS-MM.
003460     MOVE DATAGG TO DATA-SYS-GG.
003470*****APERTURA ARCHIVI
003480     PERFORM A1-010-OPEN  THRU A1-990-OPEN-EX.
003490*****CICLO ELABORATIVO
003500     PERFORM B1-010-ESITO THRU B1-990-ESITO-EX.
003510 ML-990.
003520     STOP RUN.
003530     EJECT
003540*****************************************************************
003550*                APERTURA ARCHIVI                               *
003560*****************************************************************
003570 A1-010-OPEN.
003580*    APERTURA SQUADRATURE
003590     MOVE ZERO TO CTR-LETTI-BQVFB5.
003600     MOVE ZERO TO CTR-SCRITTI-STAMPA.
003610     OPEN INPUT BQVFB5.
003620*    APERTURA STAMPA
003630     OPEN OUTPUT BQVFBP.
003640*    EFFETTUO IL CARICAMENTO DELLA TABELLA DI WORKING PROVINCE
003650     PERFORM F2-010-CAR-PROV THRU F2-990-CAR-PROV-EX.
003660 A1-990-OPEN-EX.
003670     EXIT.
003680     EJECT
003690*****************************************************************
003700* SI SCORRE IL FILE ESITO PRECEDENTEMENTE RIEMPITO DAL BQVFBE.  *
003710*                                                               *
003720* PER OGNI FILE LOGICO CONTROLLATO, NEL FILE ESITO E' PRESENTE: *
003730* - UN  RECORD DI TESTA ('EV0')                                 *
003740* - EVENTUALMENTE 'N' RECORD DI DETTAGLIO ERRORI ('EV1')        *
003750* - UN RECORD DI CODA   ('EV9')                                 *
003760*****************************************************************
003770 B1-010-ESITO.
003780**  INIZIO CICLO
003790 B1-020.
003800*    LETTURA ARCHIVIO ESITO
003810     MOVE LOW-VALUE TO W-EOF-BQVFB5.
003820     READ BQVFB5 INTO BQVFBS1 AT END
003830           MOVE HIGH-VALUE TO W-EOF-BQVFB5.
003840     IF W-EOF-BQVFB5 EQUAL HIGH-VALUE AND
003850        CTR-LETTI-BQVFB5 EQUAL ZERO
003860*       ARCHIVIO SQUADRATURE VUOTO
003870        DISPLAY 'BQVFBT-B1-020-01: ARCHIVIO BQVFB5 VUOTO'.
003880     IF W-EOF-BQVFB5 EQUAL HIGH-VALUE
003890*      FINE ELABORAZIONE ARCHIVIO SQUADRATURE
003900       GO TO B1-040.
003910 B1-030.
003920     ADD 1 TO CTR-LETTI-BQVFB5.
003930     IF CTR-LETTI-BQVFB5 EQUAL 1
003940*    IMPOSTO 'UNA-TANTUM' LA TESTATA
003950        WRITE W-REC-STAMPA FROM W-BIANCA
003960        WRITE W-REC-STAMPA FROM W-TEST-1
003970        WRITE W-REC-STAMPA FROM W-BIANCA
003980        WRITE W-REC-STAMPA FROM W-TEST-2
003990        WRITE W-REC-STAMPA FROM W-BIANCA
004000        WRITE W-REC-STAMPA FROM W-TEST-3
004010        WRITE W-REC-STAMPA FROM W-BIANCA
004020*    DATA DI ELABORAZIONE
004030        MOVE DATA-SYS-AAAA TO W-TEST-DATA-ELAB-AA
004040        MOVE DATA-SYS-MM   TO W-TEST-DATA-ELAB-MM
004050        MOVE DATA-SYS-GG   TO W-TEST-DATA-ELAB-GG
004060        WRITE W-REC-STAMPA FROM W-TEST-4
004070        WRITE W-REC-STAMPA FROM W-BIANCA
004080        MOVE  9 TO CTR-SCRITTI-STAMPA.
004090     ADD 1 TO CTR-FLUSSI
004100     PERFORM A2-010-SCRIVI-TESTA THRU A2-990-SCRIVI-TESTA-EX.
004110     PERFORM H2-010-SCRIVI-SQUAD THRU H2-990-SCRIVI-SQUAD-EX.
004120 B1-040.
004130     IF W-EOF-BQVFB5 NOT EQUAL HIGH-VALUE
004140        GO TO B1-020.
004150 B1-050.
004160*    FINE ELABORAZIONE:
004170*    ...CHIUDO GLI ARCHIVI....
004180     PERFORM B2-010-CLOSE THRU B2-990-CLOSE-EX.
004190*    ...ED EMETTO I DISPLAY STATISTICI
004200     PERFORM D2-010-DISPL THRU D2-990-DISPL-EX.
004210 B1-990-ESITO-EX.
004220     EXIT.
004230     EJECT
004240*****************************************************************
004250*                                                               *
004260*             RECORD DI TESTA ARCHIVIO ESITI                    *
004270*****************************************************************
004280 A2-010-SCRIVI-TESTA.
004290**** IMPOSTO NEI CAMPI DI STAMPA I DATI DEL FILE LOGICO
004300*
004310*    CODICE ABI
004320     MOVE BQVFBS1-COD-ABI                TO W-TEST-ABI
004330*    DESCRIZIONE ABI
004340*    abblencamento descrizione abi per indisponibilita'
004350*    del campo relativo sulla tabella
004360     MOVE SPACES                         TO  W-TEST-DESCR-ABI
004370*    DATA BONIFICO
004380     MOVE BQVFBS1-DATA-BONIF             TO W-DATA
004390     MOVE W-AAAA                         TO W-TEST-DATA-BONI-AA
004400     MOVE W-MM                           TO W-TEST-DATA-BONI-MM
004410     MOVE W-GG                           TO W-TEST-DATA-BONI-GG
004420*    PROGRESSIVO TRASMISSIONE
004430     MOVE BQVFBS1-PROG-TRASM             TO W-TEST-PRG-TRASM
004440*    TIPO INVIO
004450     IF BQVFBS1-TIPO-INVIO EQUAL 'O'
004460       MOVE 'ORDINARIO'                  TO W-TEST-TIPO-INVIO.
004470     IF BQVFBS1-TIPO-INVIO EQUAL 'C'
004480       MOVE 'CORRETTIVO'                 TO W-TEST-TIPO-INVIO.
004490     IF BQVFBS1-TIPO-INVIO EQUAL 'A'
004500       MOVE 'ANNULLAMENTO'               TO W-TEST-TIPO-INVIO.
004510*    PROGRESSIVO INVIO
004520     MOVE BQVFBS1-PROG-INVIO             TO W-TEST-PROG-INVIO
004530*****SCRIVO LE RIGHE DI STAMPA PRECEDENTEMENTE PREPARATE
004540     WRITE W-REC-STAMPA FROM W-TEST-5
004550     WRITE W-REC-STAMPA FROM W-BIANCA
004560     WRITE W-REC-STAMPA FROM W-TEST-6
004570     WRITE W-REC-STAMPA FROM W-TEST-7
004580     WRITE W-REC-STAMPA FROM W-TEST-8
004590     WRITE W-REC-STAMPA FROM W-TEST-9
004600     WRITE W-REC-STAMPA FROM W-TEST-10
004610     WRITE W-REC-STAMPA FROM W-BIANCA
004620     WRITE W-REC-STAMPA FROM W-BIANCA
004630     ADD 9 TO CTR-SCRITTI-STAMPA.
004640     MOVE BQVFBS1-NUM-SQUADR  TO W-TEST-NUM-SQUADR
004650     MOVE BQVFBS1-NUM-PROV    TO W-TEST-NUM-PROV
004660     MOVE BQVFBS1-NUM-PROV-SQUADR TO W-TEST-PROV-SQUADR
004670     WRITE W-REC-STAMPA FROM W-TEST-17-BIS
004680     WRITE W-REC-STAMPA FROM W-TEST-18-BIS
004690     WRITE W-REC-STAMPA FROM W-TEST-19-BIS
004700     WRITE W-REC-STAMPA FROM W-BIANCA
004710     ADD  4 TO CTR-SCRITTI-STAMPA.
004720 A2-990-SCRIVI-TESTA-EX.
004730     EXIT.
004740     EJECT
004750*****************************************************************
004760*                CHIUSURA ARCHIVI                               *
004770*****************************************************************
004780 B2-010-CLOSE.
004790*    CHIUSURA SQUADRATURE
004800     CLOSE BQVFB5.
004810*    CHIUSURA STAMPA
004820     CLOSE BQVFBP.
004830*
004840 B2-990-CLOSE-EX.
004850     EXIT.
004860     EJECT
004870*****************************************************************
004880*   EMISSIONE DISPLAY STATISTICI                                *
004890*****************************************************************
004900 D2-010-DISPL.
004910     DISPLAY '************************************************'
004920     DISPLAY '* NUMERO FLUSSI LOGICI ELABORATI     : '
004930             CTR-FLUSSI                                    ' *'
004940     DISPLAY '*                                              *'
004950     DISPLAY '* RIEPILOGO FINALE NUMERO RECORDS              *'
004960     DISPLAY '* LETTI      DA -SQUADRATURE-        : '
004970             CTR-LETTI-BQVFB5                              ' *'
004980     DISPLAY '* SCRITTI    IN -STAMPA-             : '
004990             CTR-SCRITTI-STAMPA                            ' *'
005000     DISPLAY '************************************************'
005010     DISPLAY '*             FINE PROGRAMMA BQVFBT            *'
005020     DISPLAY '************************************************'.
005030 D2-990-DISPL-EX.
005040     EXIT.
005050     EJECT
005060***************************************************************
005070*CARICAMENTO DELLA TABELLA DI WORKING DELLE PROVINCE
005080***************************************************************
005090 F2-010-CAR-PROV.
005100*    INIZIALIZZO LA TABELLA DI WORKING
005110     SET  W-PUNT-PROV    TO 1.
005120     MOVE HIGH-VALUE        TO W-TAB-PROV-OCC.
005130     MOVE LOW-VALUE         TO W-EOF-BQVFPR.
005140*    APERTURA ARCHIVIO PROVINCE
005150     OPEN INPUT  BQVFPR.
005160*
005170 F2-020.
005180     READ BQVFPR INTO BQVFPR1  AT END
005190               MOVE HIGH-VALUE TO W-EOF-BQVFPR.
005200     IF W-EOF-BQVFPR EQUAL LOW-VALUE
005210        GO TO F2-040.
005220 F2-030.
005230*    ARCHIVIO TERMINATO, VERIFICO LA PRESENZA DI PROVINCE
005240     GO TO F2-050.
005250 F2-040.
005260*    ESTRATTO UN CODICE: LO IMPOSTO IN TABELLA
005270     MOVE BQVFPR1-COD   TO W-TAB-PROV-COD(W-PUNT-PROV).
005280     MOVE BQVFPR1-DESCR TO W-TAB-PROV-DESCR(W-PUNT-PROV).
005290 F2-050.
005300*    VERIFICO SE CONTINUARE AD ESTRARRE LE OCCORRENZE
005310     IF W-EOF-BQVFPR EQUAL LOW-VALUE            AND
005320        W-PUNT-PROV < W-TAB-PROV-NUM-MAX
005330        SET  W-PUNT-PROV UP BY 1
005340        GO TO  F2-020.
005350 F2-060.
005360*    VERIFICA SFONDAMENTO TABELLA
005370     IF W-EOF-BQVFPR EQUAL LOW-VALUE
005380*       SE NON SONO USCITO DAL CICLO PER FINE FILE,
005390*       IL FILE DI INPUT NON E' FINITO:SI SEGNALA LO SFONDAMENTO
005400*       DELLA TABELLA DI WORKING.
005410        DISPLAY 'BQVFBT-F2-060-01: SUPERATO IL NUMERO MASSIMO  '
005420        DISPLAY 'BQVFBT-F2-060-01: DI ELEMENTI PER LA TABELLA  '
005430        DISPLAY 'BQVFBT-F2-060-01: DI WORKING "W-TAB-PROVINCE" '
005440        MOVE '0004'      TO ABEND-CODE
005450        CALL 'BQVFSR' USING ABEND-CODE.
005460*    CHIUSURA ARCHIVIO
005470     CLOSE BQVFPR.
005480 F2-990-CAR-PROV-EX.
005490     EXIT.
005500*****************************************************************
005510* CARICAMENTO E RICERCA DECODIFICA FLAG SEGNALAZIONE ERRORI     *
005520* LA TABELLA PREVEDE AL MASSIMO 200 OCCORRENZE, CONTRO GLI 82   *
005530* VALORI AL MOMENTO PREVISTI.                                   *
005540*****************************************************************
005550 H2-010-SCRIVI-SQUAD.
005560     WRITE W-REC-STAMPA FROM W-TEST-20-BIS
005570     WRITE W-REC-STAMPA FROM W-BIANCA
005580     ADD  2 TO CTR-SCRITTI-STAMPA.
005590     MOVE 1 TO W-IND-S.
005600 H2-020.
005610*    EFFETTUO IL CARICAMENTO DELLA TABELLA DI WORKING PROVINCE
005620     PERFORM E3-010-DECOD-PROV THRU E3-990-DECOD-PROV-EX.
005630     WRITE W-REC-STAMPA FROM W-TEST-32
005640     WRITE W-REC-STAMPA FROM W-BIANCA
005650     WRITE W-REC-STAMPA FROM W-TEST-33
005660     WRITE W-REC-STAMPA FROM W-BIANCA
005670     MOVE 'SALDO TOTALE'             TO W-TEST-34-VOCE.
005680     MOVE BQVFBS1-SALDO-TOT-DICH(W-IND-S) TO W-TEST-34-DIC.
005690     MOVE BQVFBS1-SALDO-TOT-CALC(W-IND-S) TO W-TEST-34-CALC.
005700     MOVE 'SQUADRATURA'              TO W-TEST-34-SIT.
005710     IF BQVFBS1-SALDO-TOT-DICH(W-IND-S) =
005720        BQVFBS1-SALDO-TOT-CALC(W-IND-S)
005730        MOVE 'DATO CORRETTO'         TO W-TEST-34-SIT.
005740     WRITE W-REC-STAMPA FROM W-TEST-34
005750     MOVE 'SALDO SEZIONE ERARIO'     TO W-TEST-34-VOCE.
005760     MOVE BQVFBS1-SALDO-ER-DICH(W-IND-S) TO W-TEST-34-DIC.
005770     MOVE BQVFBS1-SALDO-ER-CALC(W-IND-S) TO W-TEST-34-CALC.
005780     MOVE 'SQUADRATURA'              TO W-TEST-34-SIT.
005790     IF BQVFBS1-SALDO-ER-DICH(W-IND-S) =
005800        BQVFBS1-SALDO-ER-CALC(W-IND-S)
005810        MOVE 'DATO CORRETTO'         TO W-TEST-34-SIT.
005820     WRITE W-REC-STAMPA FROM W-TEST-34
005830     MOVE 'SALDO ALTRE SEZIONI'      TO W-TEST-34-VOCE.
005840     MOVE BQVFBS1-SALDO-NO-ER-DICH(W-IND-S) TO W-TEST-34-DIC.
005850     MOVE BQVFBS1-SALDO-NO-ER-CALC(W-IND-S) TO W-TEST-34-CALC.
005860     MOVE 'SQUADRATURA'              TO W-TEST-34-SIT.
005870     IF BQVFBS1-SALDO-NO-ER-DICH(W-IND-S) =
005880        BQVFBS1-SALDO-NO-ER-CALC(W-IND-S)
005890        MOVE 'DATO CORRETTO'         TO W-TEST-34-SIT.
005900     WRITE W-REC-STAMPA FROM W-TEST-34
005910     MOVE 'DELEGHE ERARIO'           TO W-TEST-34-VOCE.
005920     MOVE BQVFBS1-DELEGHE-ER-DICH(W-IND-S) TO W-TEST-34-DIC.
005930     MOVE BQVFBS1-DELEGHE-ER-CALC(W-IND-S) TO W-TEST-34-CALC.
005940     MOVE 'SQUADRATURA'              TO W-TEST-34-SIT.
005950     IF BQVFBS1-DELEGHE-ER-DICH(W-IND-S) =
005960        BQVFBS1-DELEGHE-ER-CALC(W-IND-S)
005970        MOVE 'DATO CORRETTO'         TO W-TEST-34-SIT.
005980     WRITE W-REC-STAMPA FROM W-TEST-34
005990     MOVE 'DELEGHE SENZA ERARIO'     TO W-TEST-34-VOCE.
006000     MOVE BQVFBS1-DELEGHE-NO-ER-DICH(W-IND-S) TO W-TEST-34-DIC.
006010     MOVE BQVFBS1-DELEGHE-NO-ER-CALC(W-IND-S) TO W-TEST-34-CALC.
006020     MOVE 'SQUADRATURA'              TO W-TEST-34-SIT.
006030     IF BQVFBS1-DELEGHE-NO-ER-DICH(W-IND-S) =
006040        BQVFBS1-DELEGHE-NO-ER-CALC(W-IND-S)
006050        MOVE 'DATO CORRETTO'         TO W-TEST-34-SIT.
006060     WRITE W-REC-STAMPA FROM W-TEST-34
006070     WRITE W-REC-STAMPA FROM W-BIANCA
006080     WRITE W-REC-STAMPA FROM W-BIANCA
006090     WRITE W-REC-STAMPA FROM W-BIANCA
006100     ADD 12 TO CTR-SCRITTI-STAMPA.
006110     ADD 1 TO W-IND-S.
006120     IF W-IND-S NOT >110         AND
006130        BQVFBS1-PROV(W-IND-S) NOT = SPACE AND
006140        BQVFBS1-PROV(W-IND-S) NOT = ZERO
006150       GO TO H2-020.
006160 H2-990-SCRIVI-SQUAD-EX.
006170     EXIT.
006180*****************************************************************
006190*    RICERCA DELLA PROVINCIA                                    *
006200*****************************************************************
006210 E3-010-DECOD-PROV.
006220     MOVE ZERO TO W-FLG-TROVATO.
006230     SEARCH ALL W-TAB-PROV-EL
006240        AT END MOVE 1 TO W-FLG-TROVATO
006250     WHEN W-TAB-PROV-COD(W-PUNT-PROV)
006260                               EQUAL BQVFBS1-PROV(W-IND-S)
006270        MOVE W-TAB-PROV-DESCR(W-PUNT-PROV)
006280                              TO  W-TEST-DES-PROV.
006290     IF W-FLG-TROVATO EQUAL 1
006300        DISPLAY 'BQVFBS-E3-010-01: DECODIFICA NON TROVATA '
006310                   BQVFBS1-PROV(W-IND-S)
006320        MOVE '0006' TO ABEND-CODE
006330        CALL 'BQVFSR' USING ABEND-CODE.
006340 E3-990-DECOD-PROV-EX.
006350     EXIT.
006360     EJECT
